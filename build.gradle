buildscript {
    ext {
        springBootVersion = '2.0.3.RELEASE'
        projectVersion = '1.1'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath('se.transmode.gradle:gradle-docker:1.2')
    }
}

allprojects {
    group = 'com.tomoyadeng'
    version = "${projectVersion}"
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'docker'

    sourceCompatibility = 8
    targetCompatibility = 8

    docker {
        baseImage "openjdk:8-slim"
        maintainer 'tomoyadeng@gmail.com'
    }

    task buildDocker(type: Docker, dependsOn: build) {
        applicationName = bootJar.baseName
        tagVersion = bootJar.version
        doFirst {
            copy {
                from bootJar
                into stageDir
            }
        }
        volume "/tmp"
        addFile("${bootJar.baseName}-${bootJar.version}.jar", "app.jar")
        entryPoint(Arrays.asList("java", "-Djava.security.egd=file:/dev/./urandom", "-Dspring.profiles.active=docker", "-jar", "/app.jar"))
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        compile group: 'org.projectlombok', name: 'lombok', version: '1.18.0'
        runtime('org.springframework.boot:spring-boot-devtools')
        testCompile('org.springframework.boot:spring-boot-starter-test')
    }
}
